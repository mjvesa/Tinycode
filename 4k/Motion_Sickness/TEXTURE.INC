;굅켐
;굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇
;굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇굇


;같같같같같같같같같같같같같같같같같 DATA 같같같같같같같같같같같같같같같같같같

                Seed            dw      39313
                RandVal         dw      ?
                a               dw      ?


;같같같같같같같같같같같같같같같같같 CODE2 같같같같같같같같같같같같같같같같같같
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Bump. fs - Source es - destination
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
Bump            proc
                xor di,di
@@BumpLoop:
                mov al,fs:[di+1]
                sub al,fs:[di-1]
                mov ah,fs:[di+256]
                sub ah,fs:[di-256]
                add al,ah
                add al,92
                mov es:[di],al
                inc di
                jnz @@BumpLoop
                retn
Bump            endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Smooth. es - destination
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
Smooth          proc
                xor di,di
                xor ax,ax
@@SmoothLoop:
                mov al,es:[di-1]                
                add al,es:[di+1]
                adc ah,0
                add al,es:[di-256]
                adc ah,0
                add al,es:[di+256]
                adc ah,0
                shr ax,2
                mov es:[di],al
                inc di
                jnz @@SmoothLoop
                retn
Smooth          endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;  Random:
;
;    Generates overkewl random numbers.
;
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
Random proc
                mov ax,Seed
                push bx
                push dx
                mov bx,997
                mul bx
                inc ax
                mov Seed,ax
                shr ax,7
                pop dx
                pop bx

                mov RandVal,ax

                retn
Random endp               


;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;  MakeTexture:
;
;    Shadebob texture. Whoa. 
;
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
MakeTexture    MACRO

                mov es,Texture


                xor di,di
                xor ax,ax
                mov cx,32768
                rep stosw

                mov a,100

@@DrawBobzOuterLoop:

                call Random
                mov di,RandVal
                shl di,8
                call Random
                add di,RandVal

                mov bx,-32
@@DrawBobYLoop:
                mov cx,-32
@@DrawBobXLoop:
                mov ax,cx
                imul ax
                xchg bp,ax
                mov ax,bx
                imul ax
                add ax,bp
                shr ax,7
                neg ax
                add ax,8
                cmp ax,0
                jle @@DontAdd

                add es:[di],al
@@DontAdd:
                inc di
                inc cx
                cmp cx,32
                jl @@DrawBobXLoop

                add di,192
                inc bx
                cmp bx,32
                jl @@DrawBobYLoop

                dec a
                jnz @@DrawBobzOuterLoop
ENDM

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

MakeEvoxTextures        MACRO


                mov gs,Texture
                mov fs,Texture2
                mov es,Virt
                xor si,si
@@MakeEvoxTexturesLoop:
                call Random
                mov es:[si],al
                inc si
                jnz @@MakeEvoxTexturesLoop


                call Smooth
                call Smooth
                call Smooth

                mov es,Texture2
                mov fs,Virt

                call Bump

                xor si,si
@@MakeEvoxTexturesLoop2:
                mov al,es:[si]
                shr al,4
                mov ah,al
                shl ah,4
                add al,ah
                mov es:[si],al
                inc si
                jnz @@MakeEvoxTexturesLoop2


                mov fs,Texture
                mov es,Virt
                xor si,si
@@MakeEvoxTexturesLoop3:
                mov al,gs:[si]
                shr al,2
                mov ah,gs:[si+4*256+4]
                shr ah,2
                shl ah,4
                add al,ah
                mov es:[si],al
                inc si
                jnz @@MakeEvoxTexturesLoop3

                mov es,Texture
                push ds
                mov ds,Virt
                xor di,di
                xor si,si
                mov cx,32768
                rep movsw
                pop ds

ENDM
